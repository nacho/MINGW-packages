# Maintainer: Ignacio Casal Quinteiro <icq@gnome.org>

_realname=gitg
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.19.2
pkgrel=1
arch=('any')
pkgdesc="git repository viewer for GTK+/GNOME (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-adwaita-icon-theme"
         "${MINGW_PACKAGE_PREFIX}-gtksourceview3"
         "${MINGW_PACKAGE_PREFIX}-libpeas"
         "${MINGW_PACKAGE_PREFIX}-enchant"
         "${MINGW_PACKAGE_PREFIX}-iso-codes"
         "${MINGW_PACKAGE_PREFIX}-python3-gobject"
         "${MINGW_PACKAGE_PREFIX}-gsettings-desktop-schemas"
         "${MINGW_PACKAGE_PREFIX}-libsoup"
         "${MINGW_PACKAGE_PREFIX}-libsecret"
         "${MINGW_PACKAGE_PREFIX}-gtkspell3"
         "${MINGW_PACKAGE_PREFIX}-libgit2-glib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-vala"
             "${MINGW_PACKAGE_PREFIX}-libgee"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "intltool"
             "itstool"
             "gettext-devel")
options=('strip' 'staticlibs')
license=("GPL2+")
url="https://wiki.gnome.org/Apps/Gitg"
install=${_realname}-${CARCH}.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver:0:4}/${_realname}-$pkgver.tar.xz
        0001-Add-application_support_prepare_startup.patch
        0002-Compile-with-mwindows-flag-on-windows.patch
        0003-win32-Add-ico-resource-to-the-final-binary.patch)
sha256sums=('52968be7d3dffaf20d14b25ca68773bf8ce73b32c5c8d3eac63cea0721c7a709'
            '21253e89f7a1afe848c641344e9ebc3e1f21eca0edabbe9f9de0fd9a52eb9256'
            'ee0a8333cf3074e33c8c866c9a9323953b615a16114a1e6fb649292d8dd1ae7f'
            '4a4bfbc11bbddd44b56cd031622d6e4b5a06541f37dad4f4064edeb90a8592d5')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  patch -Np1 -i "${srcdir}/0001-Add-application_support_prepare_startup.patch"
  patch -Np1 -i "${srcdir}/0002-Compile-with-mwindows-flag-on-windows.patch"
  patch -Np1 -i "${srcdir}/0003-win32-Add-ico-resource-to-the-final-binary.patch"

  #autopoint --force
  AUTOPOINT='intltoolize --automake --copy' autoreconf -f -i
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
